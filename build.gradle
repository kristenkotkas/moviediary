plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id 'org.hidetake.ssh' version '2.8.0'
}

targetCompatibility = 1.8
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile files('/libs/templ-handlebars.jar')
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.1'
    compile group: 'com.github.jknack', name: 'handlebars', version: '4.0.6'
    compile group: 'io.vertx', name: 'vertx-core', version: '3.4.1'
    compile group: 'io.vertx', name: 'vertx-web', version: '3.4.1'
    compile group: 'io.vertx', name: 'vertx-rx-java', version: '3.4.1'
    compile group: 'io.vertx', name: 'vertx-mail-client', version: '3.4.1'
    compile group: 'io.vertx', name: 'vertx-jdbc-client', version: '3.4.1'
    compile group: 'io.vertx', name: 'vertx-web-client', version: '3.4.1'
    compile group: 'mysql', name: 'mysql-connector-java', version: '6.0.5'
    compile group: 'org.pac4j', name: 'vertx-pac4j', version: '2.1.0'
    compile group: 'org.pac4j', name: 'pac4j-oauth', version: '1.9.6'

    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.6.3'
    compile 'org.codehaus.woodstox:woodstox-core-asl:4.1.4'

    // https://mvnrepository.com/artifact/io.reactivex/rxjava-computation-expressions
    compile group: 'io.reactivex', name: 'rxjava-computation-expressions', version: '0.21.0'


    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'io.vertx', name: 'vertx-unit', version: '3.4.1'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.3.1'
    testCompile group: 'xml-apis', name: 'xml-apis', version: '1.4.01'
}

configurations.all {
    resolutionStrategy {
        force 'xml-apis:xml-apis:1.4.01' //selenium dependency fix
    }
}

mainClassName = 'server.Launcher'
group 'org.movies'

shadowJar {
    baseName = 'moviediary'
    classifier = ''
}

compileJava {
    options.encoding = "UTF-8"
}

remotes {
    master {
        role 'master'
        host = 'kyngas.eu'
        user = 'kristjan'
        identity = file('C:\\grapes\\keys\\kristjan\\id_rsa')
    }
}

ssh.settings {
    knownHosts = file("/misc/known_hosts")
}

task deployMovieDiary(dependsOn: shadowJar) {
    doLast {
        ssh.run {
            session(remotes.master) {
                File server = file('C:\\Users\\Kristjan\\ownCloud\\java\\moviediary\\build\\libs\\moviediary.jar')
                if (server.exists()) {
                    put from: server, into: '/home/kristjan/Downloads/'
                }
            }
        }
        println ':deploy:done'
    }
}